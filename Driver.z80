	cpu Z80
Z80ROM_Start

    phase 0	; Set label addresses to the start of the Z80 RAM

	ld	bc,YM2612.CTRL0	; YM2612 bank 1 port
	ld	hl,YM2612.DATA0	; YM2612 data port

	if hardwareEarrape
		ld	a,2Ch
		ld	(bc),a
		ld	(hl),20h
	endif
	ld	a,DAC_ENABLE	; DAC enable address
	ld	(bc),a			; Switch to DAC enable register
	ld	(hl),80h		; Enable DAC
	dec	a				; Now a is 2Ah (DAC_IN)
	ld	(bc),a			; Switch to DAC data input

	di

	jp	Z80EndRoutineAddress

Z80Init:
	ld	de,SampleBuffer	; Reset DE
	ld	hl,YM2612.DATA0	; Reset HL
	;ld	sp,Z80.STACK	; Align stack pointer correctly
	exx
.routineAddress:
	jp	$

Init_DacAndEffects:
	ld	b,92
$$loop_DacAndEffects:
	exx
	ld	a,(de)
	ld	(hl),a
	inc	de
	inc	de
	exx
	rept 17
	nop
	endm
	djnz $$loop_DacAndEffects
	; 112 cycles per loop
	exx								; 4
	ld	a,(de)						; 7
	ld	(hl),a						; 7
	inc	de							; 6
	inc	de							; 6

	ld	b,h							; 4
	ld	c,l							; 4

	ld	hl,Loop_DacOnly				; 10
	ld	(Z80EndRoutineAddress+1),hl	; 16

	ld	l,DisableDacInjection&0FFh	; 7
	ld	(hl),2						; 10

	ld	h,b							; 4
	ld	l,c							; 4
	;exx								; 4

Init_DacAndFm:
	ld	bc,0000h
	ld	de,CommandBuffer
	ld	hl,YM2612.CTRL0
$$loop_DacAndFm:
	ld	a,(hl)
	inc	hl
	add a,(hl)
	rra
	ld	(de),a
	inc	de
	exx
	ldi
	ld	a,(de)
	ld	(hl),a
	inc	de
	dec	l
	exx
	ld	a,DAC_IN
	ld	(de),a
	djnz $$loop_DacAndFm

	exx			; 4
	ld	a,(de)	; 7
	ld	(hl),a	; 7

	inc	de		; 6
	inc	de		; 6
	ld	b,2		; 7
	djnz $		; 26
	exx			; 4
	nop			; 4
	; --- 71 cycles in total ---

	cp	0
	cp	0

Init_DacAndSn:
	ld	b,32
	ld	de,CommandBuffer
	ld	hl,Z80.PSG
$$loop_DacAndSn:
	exx
	ld	a,(de)
	ld	(hl),a
	inc	de
	inc	de
	exx
	rept 17
	nop
	endm
	djnz $$loop_DacAndSn

	exx						; 4
	ld	a,(de)				; 7
	ld	(hl),a				; 7

	inc	de					; 6
	inc	de					; 6
	
	ld	h,d					; 4
	ld	l,e					; 4
	res	4,h					; 8
	srl	h					; 8
	rr	l					; 8
	ld	(playedSamples),hl	; 16
; --- 78 cycles ---

	ld	bc,YM2612.DATA0		; 10

	cp	0
	cp	0
	nop

Loop_DacOnly:
	ld	a,(de)
DisableDacInjection:
	ld	(bc),a
	inc hl
	ld 	(playedSamples),hl
	inc	de
	inc de
	rept 10
	nop
	endm
	cp	0
	cp	0
Z80EndRoutineAddress:
	jp	$	; Will be Loop_DacOnly later on

Init_EndSound:
	ld	bc,0000h
$$loop_endSound1:
	ld	a,(de)
	ld	(hl),a
	rept 14
	nop
	endm
	inc	de
	inc de
	cp	0
	cp	0
	dec	bc
	ld	a,b
	or	c
	jp	nz,$$loop_endSound1

	di
	halt

    dephase	; The rest of the labels are mapped normally

	cpu 68000

Z80ROM_End