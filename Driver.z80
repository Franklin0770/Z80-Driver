	cpu Z80
Z80RomStart

    phase 0	; Set label addresses to the start of the Z80 RAM

	ld	de,SampleBuffer

	ld	sp,2000h	; Align stack pointer correctly

	ld	bc,4000h	; YM2612 bank 1 port
	ld	hl,4001h	; YM2612 data port
	ld	a,DAC_ENABLE; DAC enable address
	ld	(bc),a		; Switch to DAC enable register
	ld	(hl),80h	; Enable DAC
	dec	a			; Now a is 2Ah (DAC_IN)
	ld	(bc),a		; Switch to DAC data input

	im	1
	ei
	halt		; Wait until next frame

Loop:
	ld	a,(de)
	ld	(hl),a
	inc	e
	inc de
	rept 20
	nop			; Interrupt usually occurs here
	endm
	jp	(iy)

	rorg 38h - $
; VDP VBlank:
	ld	a,(de)
	ld	(hl),a
	inc	de

	; Subtract 1000h from DE
	res	4,d

	; DE >> 1
	srl	d     ; shift high, MSB gets carry
	rr	e     ; rotate low, MSB gets old D LSB

	; DE is how many samples the Z80 played
	ld	hl,playedSamplesHigh
	ld	(hl),d	; High byte
	inc l
	inc l
	ld	(hl),e	; Low byte
Wait68k:
	jp	$
.continue:
	ld	a,Wait68k
	ld	(Wait68k+1),a	; Close the infinite loop
	ld	de,SampleBuffer	; Reset DE
	ld	hl,4001h		; Reset HL
	ld	sp,2000h		; Reset SP
Z80RoutineAddress:
	ld	iy,0000h
	ei
	jp	(iy)

Loop_EndSoundInit:
	ld	c,00h	; Second byte
	ld	b,00h	; First byte
$$loop_endSound1:
	ld	a,(de)
	ld	(hl),a
	rept 16
	nop
	endm
	inc	e
	inc de
	dec	bc
	ld	a,b
	or	c
	jp	nz,$$loop_endSound1

Loop_NoDAC:
	di
	halt
	
    dephase	; The rest of the labels are mapped normally

	cpu 68000

Z80RomEnd