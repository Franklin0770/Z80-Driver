	cpu Z80
Z80RomStart

    phase 0	; Set label addresses to the start of the Z80 RAM

	ld	bc,YM2612_CTRL0	; YM2612 bank 1 port
	ld	hl,YM2612_DATA0	; YM2612 data port

	ld	a,DAC_ENABLE	; DAC enable address
	ld	(bc),a			; Switch to DAC enable register
	ld	(hl),80h		; Enable DAC
	dec	a				; Now a is 2Ah (DAC_IN)
	ld	(bc),a			; Switch to DAC data input

	ld	de,SampleBuffer	; Reset DE
	ld	sp,Z80_STACK	; Align stack pointer correctly
	di

	jp	Z80EndRoutineAddress

Z80Init:
	ld	de,SampleBuffer	; Reset DE
	ld	hl,YM2612_DATA0	; Reset HL
	;ld	sp,Z80_STACK	; Align stack pointer correctly
.routineAddress:
	jp	$

Init_DacAndEffects:
	ld	b,92
$$loop_DacAndEffects:
	ld	a,(de)
	ld	(hl),a
	inc	de
	inc	de
	rept 19
	nop
	endm
	djnz $$loop_DacAndEffects

	nop ;exx
	ld	a,(de)
	ld	(hl),a

	inc	de
	inc	de
	ld	b,3
	djnz $

	ld	bc,Loop_DacOnly				; 10 cycles
	ld	(Z80EndRoutineAddress+1),bc	; 20 cycles
	ld	c,DisableDAC&0FFh			; 7 cycles
	ld	a,2							; 7 cycles
	ld	(bc),a						; Put LD (BC),A OP code. 7 cycles
	nop ;exx
	nop

Init_DacAndFm:
	ld	b,128
$$loop_DacAndFm:
	ld	a,(de)
	ld	(hl),a
	inc	de
	inc	de
	rept 19
	nop
	endm
	djnz $$loop_DacAndFm

	nop ;exx
	ld	a,(de)
	ld	(hl),a

	inc	de
	inc	de
	ld	b,5
	djnz $
	nop ;exx
	nop

Init_DacAndSn:
	ld	b,32
$$loop_DacAndSn:
	ld	a,(de)
	ld	(hl),a
	inc	de
	inc	de
	rept 19
	nop
	endm
	djnz $$loop_DacAndSn

	nop ;exx
	ld	a,(de)
	ld	(hl),a

	inc	e
	inc	e
	
	ld	b,d
	ld	c,e
	srl	b
	rr	c
	ld	(playedSamples),bc
; --- 56 cycles ---
	nop	;exx
	ld	a,c
	
	ld	b,h
	ld	c,l

	ld	l,a
	ld	h,0

	cp	0

Loop_DacOnly:
	ld	a,(de)
DisableDAC:
	ld	(bc),a
	inc hl
	ld 	(playedSamples),hl
	inc	de
	inc de
	rept 10
	nop
	endm
	cp	0
	cp	0
Z80EndRoutineAddress:
	jp	$	; Will be Loop_DacOnly later on

Init_EndSound:
	ld	bc,0000h
$$loop_endSound1:
	ld	a,(de)
	ld	(hl),a
	rept 14
	nop
	endm
	inc	de
	inc de
	cp	0
	cp	0
	dec	bc
	ld	a,b
	or	c
	jp	nz,$$loop_endSound1

	di
	halt

    dephase	; The rest of the labels are mapped normally

	cpu 68000

Z80RomEnd