	cpu Z80
Z80RomStart

    phase 0	; Set label addresses to the start of the Z80 RAM

	ld	bc,YM2612_CTRL0	; YM2612 bank 1 port
	ld	hl,YM2612_DATA0	; YM2612 data port

	ld	a,DAC_ENABLE	; DAC enable address
	ld	(bc),a			; Switch to DAC enable register
	ld	(hl),80h		; Enable DAC
	dec	a				; Now a is 2Ah (DAC_IN)
	ld	(bc),a			; Switch to DAC data input

	di

	jp	Z80EndRoutineAddress

Z80Init:
	ld	de,SampleBuffer	; Reset DE
	ld	hl,YM2612_DATA0	; Reset HL
	;ld	sp,Z80_STACK	; Align stack pointer correctly
	exx
.routineAddress:
	jp	$

Init_DacAndEffects:
	ld	b,92
$$loop_DacAndEffects:
	exx
	ld	a,(de)
	ld	(hl),a
	inc	de
	inc	de
	exx
	rept 17
	nop
	endm
	djnz $$loop_DacAndEffects

	exx
	ld	a,(de)
	ld	(hl),a
	inc	de
	inc	de

	ld	b,h
	ld	c,l

	ld	hl,Loop_DacOnly
	ld	(Z80EndRoutineAddress+1),hl

	ld	l,DisableDacInjection&0FFh
	ld	(hl),2

	ld	h,b
	ld	l,c
	exx

Init_DacAndFm:
	ld	b,128
	ld	de,CommandBuffer
	ld	hl,YM2612_CTRL0
$$loop_DacAndFm:
	exx
	ld	a,(de)
	ld	(hl),a
	inc	de
	inc	de
	exx
	rept 17
	nop
	endm
	djnz $$loop_DacAndFm

	exx
	ld	a,(de)
	ld	(hl),a

	inc	de
	inc	de
	ld	b,2
	djnz $
	exx
	nop

Init_DacAndSn:
	ld	b,32
	ld	de,CommandBuffer
	ld	hl,PSG_PORT
$$loop_DacAndSn:
	exx
	ld	a,(de)
	ld	(hl),a
	inc	de
	inc	de
	exx
	rept 17
	nop
	endm
	djnz $$loop_DacAndSn

	exx
	ld	a,(de)
	ld	(hl),a

	inc	de
	inc	de
	
	ld	b,d
	ld	c,e
	srl	b
	rr	c
	ld	(playedSamples),bc
; --- 56 cycles ---
	exx
	ld	a,c
	
	ld	b,h
	ld	c,l

	ld	l,a
	ld	h,0

	nop

Loop_DacOnly:
	ld	a,(de)
DisableDacInjection:
	ld	(bc),a
	inc hl
	ld 	(playedSamples),hl
	inc	de
	inc de
	rept 10
	nop
	endm
	cp	0
	cp	0
Z80EndRoutineAddress:
	jp	$	; Will be Loop_DacOnly later on

Init_EndSound:
	ld	bc,0000h
$$loop_endSound1:
	ld	a,(de)
	ld	(hl),a
	rept 16
	nop
	endm
	inc	e
	inc de
	dec	bc
	ld	a,b
	or	c
	jp	nz,$$loop_endSound1

	di
	halt

    dephase	; The rest of the labels are mapped normally

	cpu 68000

Z80RomEnd