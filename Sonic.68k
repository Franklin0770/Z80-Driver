	include "Variables.asm"
	include "Constants.asm"
	include "Macros.asm"

	listing purecode ; We sure want the listing file, but only the final code in expanded macros

ROM_Start

	org 0

; ================================================================
; 68000 vectors (with its error code in square brackets, AAAAAAxx)
; ================================================================

		dc.l M68K_STACK			; Initial stack pointer value (SP value)
		dc.l EntryPoint			; Start of program (PC value)
		dc.l BusError			; Bus error							[1]
		dc.l AddressError		; Address error						[2]
		dc.l IllegalInstruction	; Illegal instruction				[3]
		dc.l DivisionByZero		; Division by zero					[4]
		dc.l CHKException		; CHK exception						[5]
		dc.l TRAPVException		; TRAPV exception					[6]
		dc.l PrivilegeViolation	; Privilege violation				[7]
		dc.l TRACEException		; TRACE exception					[8]
		dc.l LineAEmulator		; Line-A emulator					[9]
		dc.l LineFEmulator		; Line-F emulator					[10]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l SpuriousException	; Spurious exception				[11]
		dc.l InterruptRequest	; IRQ level 1						[12]
		dc.l InterruptRequest	; IRQ level 2						[12]
		dc.l InterruptRequest	; IRQ level 3 						[12]
		dc.l VDP_HBlank			; IRQ level 4 (horizontal retrace)
		dc.l InterruptRequest	; IRQ level 5						[12]
		dc.l VDP_VBlank			; IRQ level 6 (vertical retrace)
		dc.l InterruptRequest	; IRQ level 7						[12]
		dc.l TRAPException		; TRAP #00 exception				[13]
		dc.l TRAPException		; TRAP #01 exception				[13]
		dc.l TRAPException		; TRAP #02 exception				[13]
		dc.l TRAPException		; TRAP #03 exception				[13]
		dc.l TRAPException		; TRAP #04 exception				[13]
		dc.l TRAPException		; TRAP #05 exception				[13]
		dc.l TRAPException		; TRAP #06 exception				[13]
		dc.l TRAPException		; TRAP #07 exception				[13]
		dc.l TRAPException		; TRAP #08 exception				[13]
		dc.l TRAPException		; TRAP #09 exception				[13]
		dc.l TRAPException		; TRAP #10 exception				[13]
		dc.l TRAPException		; TRAP #11 exception				[13]
		dc.l TRAPException		; TRAP #12 exception				[13]
		dc.l TRAPException		; TRAP #13 exception				[13]
		dc.l TRAPException		; TRAP #14 exception				[13]
		dc.l TRAPException		; TRAP #15 exception				[13]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]
		dc.l UnknownError		; Unused (reserved)					[14]

; ===================================================================
; Mega Drive ROM header (reference: https://plutiedev.com/rom-header)
; ===================================================================

		dc.b "SEGA MEGA DRIVE "										; System type - 16 bytes
		dc.b "(C)BRO0 2025.DEC"										; Copyright, release year and month (e.g. "(C)SEGA 1991.APR") - 16 bytes
		dc.b "Codename FrankPCM: a high-tech Z80 audio driver "		; Domestic name - 48 bytes
		dc.b "Codename FrankPCM: a high-tech Z80 audio driver "		; Overseas name - 48 bytes
		dc.b "GM-12345678-00"										; Serial number ("xx-yyyyyyyy-zz") - 14 bytes
		dc.w $0000													; Where this ROM gets patched with a 16-bit checksum - 2 bytes
		dc.b "J               "										; Device support (e.g. "J" for 3-button controller) - 16 bytes
		dc.l ROM_Start												; Start address of ROM - 4 bytes
		dc.l ROM_End												; End address of ROM - 4 bytes
		dc.l $FF0000												; Start address of WRAM - 4 bytes
		dc.l $FFFFFF 												; End address of WRAM - 4 bytes
		dc.b "                                                                " ; padding for reserved space - 64 bytes
		dc.b "JU "													; Region support - 16 bytes
		dc.b "             "										; padding for reserved space (you can put a comment if you want!) - 13 bytes

; ========================
; Error handler jump table
; ========================

BusError:
	move.l	#$AAAAAAA1,d7
	stop #$2700 ; some emulators might not recognize this instruction

AddressError:
	move.l	#$AAAAAAA2,d7
	stop #$2700

IllegalInstruction:
	move.l	#$AAAAAAA3,d7
	stop #$2700

DivisionByZero:
	move.l	#$AAAAAAA4,d7
	stop #$2700

CHKException:
	move.l	#$AAAAAAA5,d7
	stop #$2700

TRAPVException:
	move.l	#$AAAAAAA6,d7
	stop #$2700

PrivilegeViolation:
	move.l	#$AAAAAAA7,d7
	stop #$2700

TRACEException:
	move.l	#$AAAAAAA8,d7
	stop #$2700

LineAEmulator:
	move.l	#$AAAAAAA9,d7
	stop #$2700

LineFEmulator:
	move.l	#$AAAAAA10,d7
	stop #$2700

SpuriousException:
	move.l	#$AAAAAA11,d7
	stop #$2700

InterruptRequest:
	move.l	#$AAAAAA12,d7
	stop #$2700

TRAPException:
	move.l	#$AAAAAA13,d7
	stop #$2700

UnknownError:
	move.l	#$AAAAAA14,d7
	stop #$2700

; ==========================

; Horizontal interrupt
VDP_HBlank:
	rte

VDP_VBlank:
	rte

; ==========================

EntryPoint:
; ============================
; VDP initialization and setup
; ============================

	lea	VDP_CTRL,a0

	tst.w 	(a0) ; Testing the VDP control port safely resets it
	
	move.l  #(VDPREG_MODE1|%00000100)<<16|(VDPREG_MODE2|%01100100),(a0)	; Mode register #1 and Mode register #2
    move.l  #(VDPREG_MODE3|%00000000)<<16|(VDPREG_MODE4|%10000001),(a0)	; Mode register #3 and Mode Register #4
    
    move.l  #VDPREG_PLANEA|(VRAM_PLANEA>>10)<<16|(VDPREG_PLANEB|(VRAM_PLANEB>>13)),(a0)	; Plane A and Plane B address
	move.l  #VDPREG_SPRITE|(VRAM_SPRITE>>9)<<16|(VDPREG_WINDOW|(VRAM_WINDOW>>10)),(a0)	; Sprite and Window address
    move.w  #VDPREG_HSCROLL|(VRAM_HSCROLL>>10),(a0)										; Horizontal scroll address
    
    move.l  #(VDPREG_WINX|$00)<<16|(VDPREG_WINY|$00),(a0)			; Window X split and Window Y split
    move.l  #(VDPREG_SIZE|%00000001)<<16|(VDPREG_BGCOL|$00),(a0)	; Tilemap size and Background color
    move.l  #(VDPREG_INCR|$02)<<16|(VDPREG_HRATE|$FF),(a0)			; Autoincrement and HBlank IRQ rate

	lea Z80_BUSREQ,a3
	lea Z80_RESET,a2

	move.w	#$100,d2	; Assert, stop
	moveq	#0,d1		; Deassert, start

	assertZ80Reset d1,a2	; Assert reset
	stopZ80 d2,a3			; Request bus
	deassertZ80Reset d2,a2	; Release reset

; We can do more stuff while the Z80 is theoretically stopping
	lea Z80RomStart,a0
	lea Z80_WRAM,a1
	move.w	#(Z80RomEnd-Z80RomStart)-1,d0	; Mustn't go over $7F

$$copy_program2:
	move.b	(a0)+,(a1)+
	dbf d0,$$copy_program2

	assertZ80Reset d2,a2	; Assert reset

	moveq	#20,d0
$$wait:	; Wait for the YM2612
    dbf	d0,$$wait

	deassertZ80Reset d2,a2	; Release reset

	startZ80 d1,a3			; Release bus

	stop	#$2700

	include "Driver.z80"

	align 2	; Even out the PC
Music:
	;binclude "continuity_test.bin" ; For debugging: test with increasing bytes to make sure no samples get skipped
	;binclude "24. Time Rift Shift ~ Vs. Metal Sonic.raw" ; Great music right here
	binclude "Don't Stand So Close To Me.raw" ; Another banger
	;binclude "music.pcm" ; Let's not talk about this
	;binclude "My Summary.raw"

ROM_End