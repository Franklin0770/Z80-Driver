; Naming conventions:
; variableValue
; CONSTANT_VALUE
; MainRoutineAndLables
; sub_routine

	include "Variables.asm"
	include "Constants.asm"
	include "Macros.asm"

	listing purecode ; We sure want the listing file, but only the final code in expanded macros

; These switches enable the entire purpose of this driver and disabling them likely makes the playback similar to "traditional" DAC drivers
smoothPlayback:	equ TRUE	; Put it to FALSE to prevent 68k output during buffer load
accurateSpeed:	equ TRUE	; Put it to FALSE to ignore 68k sample increment calculation
gensFix:		equ FALSE	; Since Gens doesn't support the STOP instruction, put it to TRUE to replace it

	org 0

RomStart ; Vectors
		dc.l SYS_STACK			; Initial stack pointer value (SP value)
		dc.l EntryPoint			; Start of program (PC value)
		dc.l BusError			; Bus error
		dc.l AddressError		; Address error
		dc.l IllegalInstruction	; Illegal instruction
		dc.l DivisionByZero		; Division by zero
		dc.l CHKException		; CHK exception
		dc.l TRAPVException		; TRAPV exception
		dc.l PrivilegeViolation	; Privilege violation
		dc.l TRACEException		; TRACE exception
		dc.l LineAEmulator		; Line-A emulator
		dc.l LineFEmulator		; Line-F emulator
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l SpuriousException	; Spurious exception
		dc.l GenericError		; IRQ level 1
		dc.l GenericError		; IRQ level 2
		dc.l GenericError		; IRQ level 3 
		dc.l GenericError		; IRQ level 4 (horizontal retrace interrupt)
		dc.l GenericError		; IRQ level 5
		dc.l VDP_VBlank			; IRQ level 6 (vertical retrace interrupt)
		dc.l GenericError		; IRQ level 7
		dc.l GenericError		; TRAP #00 exception
		dc.l GenericError		; TRAP #01 exception
		dc.l GenericError		; TRAP #02 exception
		dc.l GenericError		; TRAP #03 exception
		dc.l GenericError		; TRAP #04 exception
		dc.l GenericError		; TRAP #05 exception
		dc.l GenericError		; TRAP #06 exception
		dc.l GenericError		; TRAP #07 exception
		dc.l GenericError		; TRAP #08 exception
		dc.l GenericError		; TRAP #09 exception
		dc.l GenericError		; TRAP #10 exception
		dc.l GenericError		; TRAP #11 exception
		dc.l GenericError		; TRAP #12 exception
		dc.l GenericError		; TRAP #13 exception
		dc.l GenericError		; TRAP #14 exception
		dc.l GenericError		; TRAP #15 exception
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)
		dc.l GenericError		; Unused (reserved)

; ROM header
		dc.b "SEGA MEGA DRIVE",$20				; "$20" is padding
		dc.b "(C)BRO0 2024.OCT"					; Copyright(-ish), release year and month
		dc.b "Codename FrankPCM: a high-tech audio driver"	; Domestic name
		dc.b "     "							; padding
		dc.b "Codename FrankPCM: a high-tech audio driver"	; Domestic name
		dc.b "     "							; padding
		dc.b "AI-23456786-00"					; Serial number (I mashed the keyboard for this)
		dc.w $0000								; Empty checksum
		dc.b "J"								; Joypad type
		dc.b "               "					; padding
		dc.l RomStart							; Start address of ROM
		dc.l RomEnd								; End address of ROM
		dc.l $FF0000							; Start address of WRAM
		dc.l $FFFFFF 							; End address of WRAM
		dc.b "                                                                "
		dc.b "JU "								; Region support
		dc.b "             "					; padding for reserved space

; Error handler jump table

BusError:
	moveq	#1,d7
	stop #$2700

AddressError:
	moveq	#2,d7
	stop #$2700

IllegalInstruction:
	moveq	#3,d7
	stop #$2700

DivisionByZero:
	moveq	#4,d7
	stop #$2700

CHKException:
	moveq	#5,d7
	stop #$2700

TRAPVException:
	moveq	#6,d7
	stop #$2700

PrivilegeViolation:
	moveq	#7,d7
	stop #$2700

TRACEException:
	moveq	#8,d7
	stop #$2700

LineAEmulator:
	moveq	#9,d7
	stop #$2700

LineFEmulator:
	moveq	#10,d7
	stop #$2700

SpuriousException:
	moveq	#11,d7
	stop #$2700

GenericError:
	moveq	#12,d7
	stop #$2700

; A2: live samples, A0: Z80 samples for later
; 240 68k cycles for every 32 kHz sample

VDP_VBlank:
; --- ~20 cycles in total ---
	lea	YM2612_DATA|Z80_RAM,a3			; 8 cycles
	lea	playedSamplesHigh|Z80_RAM,a4	; 8 cycles
	lea Z80_BUSREQ,a5					; 8 cycles
	move.w	#$100,d1					; 8 cycles
	move.l	(sampleIndex),d2			; 12 cycles
	moveq	#0,d3						; 4 cycles
; --- 68 cycles in total ---
	moveq	#8,d0		; The high 16-bit portion clears, 4 cycles
$$z80_wait:
	dbf d0,$$z80_wait	; 114 cycles
; --- 186 cycles in total ---
	stopZ80 d1,a5		; Initiate Z80 stop, 8 cycles
	waitZ80	d3,a5		; 12 cycles (at best)
; --- 206 cycles in total ---
	move.w	(a4)+,d0	; Get high byte of playedSamples, 8 cycles
	move.b	(a4),d0		; Get lower byte of playedSamples, 8 cycles
	add.l	d0,d2		; 8 cycles
	movea.l	d2,a2		; Sample index while executing 68k routine, 4 cycles
; --- 234 cycles in total ---
	if smoothPlayback
		move.b	(a2)+,(a3)	; Output sample, 12 cycles
	elseif accurateSpeed
		addq.l	#1,a2
		nop
	endif

; --- Cycle count restarts ---

	movea.l	d2,a0	; 4 cycles

	if accurateSpeed
		addaq	21+2,a0	; Sample index after 68k routine execution (A2 at the end, basically), 8 cycles
		move.l	a0,d2	; D2 is now the updated sample index, 4 cycles
	endif

	lea	SampleBuffer|Z80_RAM,a1	; 8 cycles
; --- 24 cycles in total ---
	btst	#0,d2				; Is the sample index even? 6 cycles
	beq.s	$$skip_alignment	; If yes, skip the alignment, 10 cycles
	move.b	(a0)+,(a1)			; If not, the 68k will crash, so we avoid it. 12 cycles
	addq.l	#2,a1
	move.l	a0,d2	; D2 is now the updated sample index, 4 cycles
$$skip_alignment:
	nop			; 4 cycles
	nop			; 4 cycles
	nop			; 4 cycles
	move.l	#RomEnd,d1			; 12 cycles
	cmp.l	d2,d1				; Is the audio about to end (ends on Z80 buffer)? 8 cycles
; --- 72 cycles in total ---
	bge.s	$$skip_tracking		; 10 cycles on branch, 8 cycles no branch
	; Z80 playback with sample tracking routine
	move.b	#Loop_EndSoundInit,(Z80RoutineAddress+2)|Z80_RAM	; Little endian, 16 cycles
	sub.l	d1,d2	; D2 is now the distance to the end of the audio (after 68k execution), 8 cycles
	subq.l	#1,d2
	move.b	d2,(Loop_EndSoundInit+1|Z80_RAM); 16 cycles
	lsr.w	#8,d2							; 22 cycles
	move.b	d2,(Loop_EndSoundInit+3|Z80_RAM); 16 cycles
	move.b	#1,shouldStop					; 16 cycles
	bra.s	$$continue						; 10 cycles
; --- 82 cycles in total ---
$$skip_tracking:
	; Normal Z80 playback routine
	move.b	#Loop,(Z80RoutineAddress+2)|Z80_RAM	; Little endian, 16 cycles

$$continue:
; --- 98 cycles in total ---
	moveq	#4,d0	; 4 cycles
$$timing_wait1:		; 54 cycles
	dbf d0,$$timing_wait1
; --- 156 cycles in total ---

	loadSamples	; D0 and D1 break here

	if smoothPlayback
		move.b	(a2)+,(a3)	; Output sample, 12 cycles
	elseif accurateSpeed
		addq.l	#1,a2
		nop
	endif

; --- Cycle count restarts ---
	move.b	#Wait68k.continue,((Wait68k+1)|Z80_RAM)	; 20 cycles
	move.l	d2,(sampleIndex)	; 20 cycles

	moveq	#7,d0
$$timing_wait2:
	dbf	d0,$$timing_wait2

	startZ80 a5	; Restart Z80

	; DPLC loading queues
	rte


EntryPoint:
	;move.l	#"SEGA",($00A14000)

	lea		VDP_CTRL,a0

; VDP register setup
	move.l  #(VDPREG_MODE1|%00000100)<<16|VDPREG_MODE2|%00100100,(a0)	; Mode register #1 and Mode register #2
	move.l  #(VDPREG_MODE3|%00000000)<<16|VDPREG_MODE4|%10000001,(a0)	; Mode register #3 and Mode register #4
	
	move.l  #(VDPREG_PLANEA|(PLANEA_ADDR>>10))<<16|VDPREG_PLANEB|(PLANEB_ADDR>>13),(a0)	; Plane A and Plane B address
	move.l  #(VDPREG_SPRITE|(SPRITE_ADDR>>9))<<16|VDPREG_WINDOW|(WINDOW_ADDR>>10),(a0)	; Sprite and Window address
    move.w  #VDPREG_HSCROLL|(HSCROLL_ADDR>>10),(a0)										; HScroll address
    
    move.l  #(VDPREG_SIZE|$00)<<16|VDPREG_WINX|$00,(a0)		; Tilemap size and Window X split
    move.l  #(VDPREG_WINY|$00)<<16|VDPREG_INCR|$00,(a0)		; Window Y split and Autoincrement
    move.l  #(VDPREG_BGCOL|$00)<<16|VDPREG_HRATE|$FF,(a0)	; Background color and HBlank IRQ rate

$$JOY_Setup
	moveq	#$40,d0
	move.b 	d0,(JOY_CTRL)
	move.b 	d0,(JOY_DATA)

; Start of ROM code

	lea Z80_BUSREQ,a3
	lea Z80_RESET,a2

	move.w	#$100,d2	; Assert, stop
	moveq	#0,d1		; Deassert, start

	assertZ80Reset d1,a2	; Assert reset
	stopZ80 d2,a3			; Request bus
	deassertZ80Reset d2,a2	; Release reset

; We can do more stuff while the Z80 is theoretically stopping
	lea Z80RomStart,a0
	lea Z80_RAM,a1
	move.l 	#Music,(sampleIndex)
	move.w	#(Z80RomEnd-Z80RomStart)-1,d0	; Mustn't go over $7F

$$copy_program:
	move.b	(a0)+,(a1)+
	dbf d0,$$copy_program

	clr.b	(playedSamplesHigh|Z80_RAM)
	clr.b	(playedSamplesLow|Z80_RAM)
	clr.b	(shouldStop)

	assertZ80Reset d2,a2	; Assert reset

	moveq	#20,d0
$$wait:	; Wait for the YM2612
    dbf	d0,$$wait

	deassertZ80Reset d2,a2	; Release reset

	;loopTest 53

	startZ80 d1,a3			; Release bus

ReadJoypad:
	lea	JOY_DATA,a0
	move.b 	#$40,(a0)	; write $40 to request controller state
	nop	; wait for the bus to get the controller state
	nop
	nop
	nop
	move.b	(a0),d0	; put control state somewhere
	btst 	#JOY_C,d0
	beq.w	PlaySample

	if gensFix
		move #$2500,sr
	endif
HaltCPU:
	tst.b	(shouldStop)
	bne.s	$$disableCPU
	if gensFix = FALSE
		stop	#$2500
	else
		bra.s *
	endif
	bra.s	ReadJoypad
$$disableCPU:
	if gensFix = FALSE
		stop	#$2700
	else
		move #$2700,sr
		bra.s *
	endif

PlaySample:
	move.l	#Music,(sampleIndex)
	bra.s	HaltCPU

	include "Driver.z80"

Music:
	;binclude "continuity_test.bin" ; Test with increasing bytes to make sure no samples get skipped
	;binclude "24. Time Rift Shift ~ Vs. Metal Sonic.raw" ; Great music right here
	;binclude "Don't Stand So Close To Me.raw" ; Another banger
	binclude "Spirits In The Material World.raw" ; This is a good one
	;binclude "Every Little Thing She Does Is Magic.raw" ; Another nice one
	;binclude "Ki.raw" ; minceraft
	;binclude "Droopy likes your face.raw"
	;binclude "Chris.raw"
	;binclude "music.pcm" ; Let's not talk about this
	;binclude "My Summary.raw"

RomEnd